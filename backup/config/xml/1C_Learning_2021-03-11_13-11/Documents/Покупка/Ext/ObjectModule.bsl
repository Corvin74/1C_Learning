
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Проверяем, нужно-ли проводить документ(содержит-ли он табличную часть)
	Если Продукты.Количество() = 0 Тогда
		Сообщить("Внимание! Документ не содержит строк! Проведение невозможно!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Установим флаг сохранения движений по регистру Продукты
	Движения.Продукты.Записывать = Истина;
	
	// Выбираем данные из документа с помощью запроса
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПокупкаПродукты.Продукт КАК Продукт,
	               |	СУММА(ПокупкаПродукты.Количество) КАК Количество,
	               |	СУММА(ПокупкаПродукты.Количество * ПокупкаПродукты.Цена) КАК Сумма
	               |ИЗ
	               |	Документ.Покупка.Продукты КАК ПокупкаПродукты
	               |ГДЕ
	               |	ПокупкаПродукты.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПокупкаПродукты.Продукт
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Продукт
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	// Формируем движения/записи регистра
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.Продукты.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период      = Дата;
		Движение.Продукт     = ВыборкаДетальныеЗаписи.Продукт;
		Движение.Количество  = ВыборкаДетальныеЗаписи.Количество;
		Движение.Сумма       = ВыборкаДетальныеЗаписи.Сумма;

	КонецЦикла;
	
КонецПроцедуры
